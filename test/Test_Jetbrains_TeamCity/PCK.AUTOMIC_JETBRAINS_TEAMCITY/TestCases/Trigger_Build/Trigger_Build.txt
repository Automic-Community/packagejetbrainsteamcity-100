*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        trigger_build
Default Tags
Test Template     Trigger Build Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TeamCityURL        Username                Password                BuildConfig
Trigger Build with Valid Inputs
                      OK        ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    ${Config_Build}

Assertion with Invalid TeamCity URL
                      NOK       Invalid            ${Username_TeamCity}    ${Password_TeamCity}    ${Config_Build}

Assertion with Non-Existing Username
                      NOK       ${URL_TeamCity}    Invalid                 ${Password_TeamCity}    ${Config_Build}

Assertion with Invalid Password
                      NOK       ${URL_TeamCity}    ${Username_TeamCity}    Invalid                 ${Config_Build}

Assertion with Non-Existing Build Configuration
                      NOK       ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    Invalid

Assert TeamCity URL as Mandatory field
                      OTHER     ${EMPTY}           ${Username_TeamCity}    ${Password_TeamCity}    ${Config_Build}

Assert Username as Mandatory field
                      OTHER     ${URL_TeamCity}    ${EMPTY}                ${Password_TeamCity}    ${Config_Build}

Assert Password as Mandatory field
                      OTHER     ${URL_TeamCity}    ${Username_TeamCity}    ${EMPTY}                ${Config_Build}

Assert Build Configuration as Mandatory field
                      OTHER     ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "TRIGGER BUILD"*******
    Log    *******Connect to AE*******
    Connect AE

Trigger Build Template
    [Arguments]    ${OK_NOK}    ${TeamCity_URL}    ${Username}    ${Password}    ${Build_Config}
    [Documentation]    This test will trigger a build on TeamCity.
    ...    Preconditions: Used TeamCity URL and Build Configuration should exist.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${TeamCity_URL}: This field specifies TeamCity URL.
    ...    -${Username}: This field specifies Username for TeamCity.
    ...    -${Password}: This field specifies the password for TeamCity user.
    ...    -${Build_Config}: This field specifies configuration of the build which you want to trigger.
    Generate String
    Action Create    ${_TRIGGER_BUILD_ACTION}
    TeamCity Credentials
    Run Keyword If    '${Build_Config}' == 'Invalid'    Action Set    &UC4RB_TMC_BUILD_CONFIG#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TMC_BUILD_CONFIG#    ${Build_Config}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TMC_BUILD_ID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TMC_BUILD_ID#

Teardown
    Log    *******End Test "TRIGGER BUILD"*******
