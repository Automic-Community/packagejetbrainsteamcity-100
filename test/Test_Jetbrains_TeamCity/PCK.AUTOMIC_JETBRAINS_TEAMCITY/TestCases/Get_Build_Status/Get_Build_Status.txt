*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_build_status
Default Tags
Test Template     Get Build Status Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    TeamCityURL        Username                Password                BuildID
Get Build Status with Build ID also Asserting Username and Password as Optional Field
                      OK        ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    ${ID_Build}

Get Build Status while Providing Username and Password
                      OK        ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    ${ID_Build}

Assertion with Invalid TeamCity URL
                      NOK       Invalid            ${Username_TeamCity}    ${Password_TeamCity}    ${ID_Build}

Assertion with Non-Existing Username
                      NOK       ${URL_TeamCity}    Invalid                 ${Password_TeamCity}    ${ID_Build}

Assertion with Invalid Password
                      NOK       ${URL_TeamCity}    ${Username_TeamCity}    Invalid                 ${ID_Build}

Assertion with Non-Existing Build ID
                      NOK       ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    Invalid

Assert Behavior of Action when Username is Provided and Password is not Provided
                      NOK       ${URL_TeamCity}    ${Username_TeamCity}    ${EMPTY}                ${ID_Build}

Assert Behavior of Action when Username is not Provided and Password is Provided
                      NOK       ${URL_TeamCity}    ${EMPTY}                ${Password_TeamCity}    ${ID_Build}

Assert TeamCity URL as Mandatory field
                      OTHER     ${EMPTY}           ${Username_TeamCity}    ${Password_TeamCity}    ${ID_Build}

Assert Build ID as Mandatory field
                      OTHER     ${URL_TeamCity}    ${Username_TeamCity}    ${Password_TeamCity}    ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "GET BUILD STATUS"*******
    Log    *******Connect to AE*******
    Connect AE

Get Build Status Template
    [Arguments]    ${OK_NOK}    ${TeamCity_URL}    ${Username}    ${Password}    ${Build_ID}
    [Documentation]    This test will retrieves the build status from TeamCity.
    ...    Preconditions: Used TeamCity URL and Build ID should exist.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${TeamCity_URL}: This field specifies TeamCity URL.
    ...    -${Username}: This field specifies Username for TeamCity.
    ...    -${Password}: This field specifies the password for TeamCity user.
    ...    -${Build_ID}: This field specifies id of the build whose status you want to retrieve.
    Generate String
    Action Create    ${_GET_BUILD_STATUS_ACTION}
    TeamCity Credentials
    Run Keyword If    '${Build_ID}' == 'Invalid'    Action Set    &UC4RB_TMC_BUILD_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TMC_BUILD_ID#    ${Build_ID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure    ELSE
    ...    Assert Mandatory
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TMC_BUILD_STATUS#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TMC_BUILD_STATUS#

Teardown
    Log    *******End Test "GET BUILD STATUS"*******
